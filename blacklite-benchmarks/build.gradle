plugins {
    id 'java'
}

sourceSets {
    jmh {
        java.srcDirs = ['src/jmh/java']
        resources.srcDirs = ['src/jmh/resources']
        compileClasspath += sourceSets.main.runtimeClasspath
    }
}

tasks.withType(PublishToMavenRepository).configureEach { it.enabled = false }

// https://gist.github.com/mrbald/7bbb5113c3164d5243314da8c28649a1
dependencies {
    implementation project(':blacklite-core')
    implementation project(':blacklite-codec-zstd')

    jmhImplementation 'org.openjdk.jmh:jmh-core:1.23'
    jmhAnnotationProcessor 'org.openjdk.jmh:jmh-generator-annprocess:1.23'

    // https://mvnrepository.com/artifact/org.xerial/sqlite-jdbc
    implementation group: 'org.xerial', name: 'sqlite-jdbc', version: '3.32.3.2'
}

task jmh(type: JavaExec, dependsOn: jmhClasses) {
    mainClass = 'org.openjdk.jmh.Main'
    classpath = sourceSets.jmh.compileClasspath + sourceSets.jmh.runtimeClasspath

    // To enable the built-in stacktrace sampling profiler
    //args = ['-prof', 'stack']
    //args = ['-prof', 'gc']

    // easier to use the oracle JDK because it has debug symbols available
    // https://github.com/jvm-profiling-tools/async-profiler#installing-debug-symbols
    // sdk use java 17.0.3-oracle
    args = ['-prof', 'async:output=flamegraph']
    //args = ['-prof', 'gc', '-rf', 'json']
    //args = ['-prof', 'async:output=async-profiler.txt;alloc=2m']
}

classes.finalizedBy(jmhClasses)
